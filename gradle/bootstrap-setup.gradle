/*
 * Root project configuration that is reused by subprojects to apply the Xtend compiler.
 */

// The repositories to query when constructing the Xtend compiler classpath
repositories {
	jcenter()
	maven {
		name 'xtend-bootstrap'
		url 'http://services.typefox.io/open-source/jenkins/job/xtend-bootstrap/lastStableBuild/artifact/build-result/maven-repository/'
	}
	// maven {
	// 	url 'http://services.typefox.io/open-source/jenkins/job/xtext-lib/job/sz_bom/lastSuccessfulBuild/artifact/build/maven-repository/'
	// }
}

// The Xtend compiler version to use
def bootstrapXtendVersion = '2.14.0'

configurations {
	xtendCompiler {
		description 'Bootstrap dependencies for the Xtend compiler'
		resolutionStrategy {
			eachDependency {
				if (requested.group == 'org.eclipse.xtext' || requested.group == 'org.eclipse.xtend')
					useVersion(bootstrapXtendVersion)
				if (requested.group == 'com.google.inject' && requested.name == 'guice')
					useVersion(versions.guice)
				if (requested.group == 'org.eclipse.jdt' && requested.name == 'org.eclipse.jdt.core')
					useVersion(versions.jdtCore)
			}
		}
		exclude group: 'asm'
	}
}

dependencies {
	xtendCompiler "org.eclipse.xtend:org.eclipse.xtend.core:$bootstrapXtendVersion"
	xtendCompiler "org.eclipse.xtext:org.eclipse.xtext.smap:$bootstrapXtendVersion"
	xtendCompiler "org.xtext:xtext-gradle-builder:$versions.xtext_gradle_plugin"
}
